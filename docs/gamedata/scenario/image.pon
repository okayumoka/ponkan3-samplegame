#################################################################
# 背景・立ち絵表示
#################################################################
*start
|背景・立ち絵表示

;full_mes
;layopt lay: "message", visible: true

【背景・立ち絵表示】
;lbrbr

Ponkan3では、キャラクターの立ち絵や背景画像を特に区別しません。$
すべて「画像を表示する」コマンド(image)で表示します。
;lbrbr

画像はすべて「レイヤー」に表示します。
;lbr
レイヤーは番号で管理されており、若い番号から順番に画面に出力されます。$
0番が最も後ろに表示され、その上に1番、2番……といった順番です。
;lbrbr

背景画像は一番後ろに表示するべきなので、通常はレイヤー0に表示します。
;lbr
キャラクターの立ち絵などはそれよりも前に表示するべきなので、レイヤー1以降に表示します。
;pcl

では少し試してみましょう。
;lbrbr

現在、レイヤー0に表示されている背景画像（公園・昼）を変更します。
;lbr
;image lay: "base", file: "image/bg02.jpg", visible: true
変更しました。夕方になりましたね！
;lbrbr

次に、レイヤー1にキャラクターを表示してみます。
;lbr
;image lay: "chara1", file: "image/chara01.png", visible: true, x: (1280-244)/2, y: 720-600
表示しました。
;lbr
続けて、レイヤー2に別のキャラクターを表示してみます。
;lbr
;image lay: "chara2", file: "image/chara02.png", visible: true, x: (1280-400)/2, y: 720-700
表示しました。$
レイヤー2のほうが上に重なるように表示されているのがわかると思います。
;lbr
;move {
  lay: "chara1",
  time: 500, ease: "out",
  path: [{x: 350-(244/2), y: 720-600, alpha: 1}]
}
;move {
  lay: "chara2",
  time: 500, ease: "out",
  path: [{x: 1280-350-(400/2), y: 720-700, alpha: 1}]
}
;waitmove
見えづらいので位置を移動しました。
;pcl

レイヤーの概念は、Ponkan3を使用する上でとても重要です。$
詳細についてはPonkan3マニュアルを参照してください。
;p

;jump file: "scenario/sample-game.pon", label: "show_menu"