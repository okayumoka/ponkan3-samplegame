#=========================================================================================
# 確認ダイアログ
#
# 使い方：
#   - 呼び出す前に tv.dialogMessage に表示したいメッセージを格納する。
#   - ;call file: "script/confirm_dialog.pon" で呼び出す
#   - クリック結果は tv.dialogResult に入っている。
#     - 「はい」が true、「いいえ」が false
#=========================================================================================
*show_dialog
;rclick enabled: false

---
tv.dialog = {
  text: {
    centerX: 1280/2,
    centerY: 310
  },
  bg: {
    file: "image/dialog.png",
    width: 1280,
    height: 720
  },
  yes: {
    file: "image/dialog_yes.png",
    x: 1280/2-200-15,
    y: 380
  },
  no: {
    file: "image/dialog_no.png",
    x: 1280/2+15,
    y: 380
  },
  baseLay: tv.layer.dialog,
}
---

;lockbuttons lay: "all", page: "fore"
;clear lay: "&tv.dialog.baseLay"
;clearbuttons lay: "&tv.dialog.baseLay"

;image {
  lay:      "&tv.dialog.baseLay",
  file:     "&tv.dialog.bg.file",
  x:        "&(ponkan.config.width - tv.dialog.bg.width) / 2",
  y:        "&(ponkan.config.height - tv.dialog.bg.height) / 2",
}
;imagebutton {
  lay:       "&tv.dialog.baseLay",
  imagefile: "&tv.dialog.yes.file",
  x:         "&tv.dialog.yes.x",
  y:         "&tv.dialog.yes.y",
  exp:      "tv.dialogResult = true",
  jump:     true,
  label:    "dialog_return",
}
;imagebutton {
  lay:       "&tv.dialog.baseLay",
  imagefile: "&tv.dialog.no.file",
  x:         "&tv.dialog.no.x",
  y:         "&tv.dialog.no.y",
  exp:      "tv.dialogResult = false",
  jump:     true,
  label:    "dialog_return",
}

;nowait
;mesopt {
  lay:      "&tv.dialog.baseLay",
  color:    0x000000,
  align:    "center",
  fontsize: 26
}
;locate lay: "&tv.dialog.baseLay", x: "&tv.dialog.text.centerX", y: "&tv.dialog.text.centerY"
;ch lay: "&tv.dialog.baseLay", text: "&tv.dialogMessage"
;endnowait
;layopt lay: "&tv.dialog.baseLay", visible: true
;unlockbuttons lay: "&tv.dialog.baseLay"
;s

*dialog_return
;lockbuttons lay: "&tv.dialog.baseLay"
# ;unlockbuttons lay: "&tv.btnLay"
;layopt lay: "&tv.dialog.baseLay", visible: false
-tv.dialogMessage = "";
;rclick enabled: true
;return

